use date
use regex
use std

const lparsedate = {dtstr               /* Parse date, using Myrddin's standard library. */
	match date.parsefmt("%Y %m %d", dtstr)
	| `std.Ok dt:	-> dt
	| `std.Err m:	std.fatal("Bad date '{}': {}\n", dtstr, m)
	;;
}

const zparsedate = {dtstr               /* Parse date, using Zeller's congruence. */
	const parseint = {istr
		match std.intparse(istr)
		| `std.Some n:	-> n
		| `std.None:	std.fatal("Bad int '{}'\n", istr)
		;;
	}

	const parsedate = {dtstr
		var dtrx = std.try(regex.compile("(\\d+)\\s+(\\d+)\\s+(\\d+)"))
		match regex.exec(dtrx, dtstr)
		| `std.Some matches:    /* matches[0] is the full text that was matched */
			if matches.len == 4
				var y = parseint(matches[1])
				var m = parseint(matches[2])
				var d = parseint(matches[3])
				regex.matchfree(matches)
				-> `std.Ok (y, m, d)
			else
				-> `std.Err "Bad date"
			;;
		| `std.None:
			-> `std.Err "Bad date"
		;;
	}

	match parsedate(dtstr)
	| `std.Ok (y, m, q):		/* year, month (1..12), day of the month (1..31) */
		if m < 3		/* special process January and February: */
		    y--; m += 12	/* they are counted as months 13 and 14 of the previous year */
		;;
		var K = y % 100		/* year of the century */
		var J = y / 100		/* zero-based century */
		var h = (q + 13 * (m + 1) / 5 + K + K / 4 + J / 4 + 5 * J) % 7
		-> h			/* h is the day of the week (0 = Saturday, 1 = Sunday, ...) */
	| `std.Err msg:
		std.fatal("Error {} parsing date for '{}'\n", msg, dtstr)
	;;
}

const lputday = {dtstr, dt              /* Print date as parsed by Myrddin's standard library. */
	std.put("According to Myrddin's lib, for date {}, the weekday is {f=%A}\n", \
		dtstr, dt)
}

const zputday = {dtstr, dow             /* Print date as parsed by Zeller's congruence. */
	const daystr = [
		"Saturday", "Sunday", "Monday", "Tuesday", "Wednesday", "Thursday", "Friday"
	]
	std.put("According to Zeller's congruence, for date {}, the weekday is {}\n", \
		dtstr, daystr[dow])
}

const main = {args : byte[:][:]
	for var i = 1; i < args.len; i++ /* 1st element of the args slice is the program name. */
		var dtstr = args[i]
		lputday(dtstr, lparsedate(dtstr))
		zputday(dtstr, zparsedate(dtstr))
	;;
}

// Output:
/*
According to Myrddin's lib, for date 2017 10 30, the weekday is Sunday
According to Zeller's congruence, for date 2017 10 30, the weekday is Monday
According to Myrddin's lib, for date 2016 2 29, the weekday is Sunday
According to Zeller's congruence, for date 2016 2 29, the weekday is Monday
According to Myrddin's lib, for date 2015 2 28, the weekday is Sunday
According to Zeller's congruence, for date 2015 2 28, the weekday is Saturday
According to Myrddin's lib, for date 29 4 12, the weekday is Sunday
According to Zeller's congruence, for date 29 4 12, the weekday is Thursday
According to Myrddin's lib, for date 570 11 30, the weekday is Sunday
According to Zeller's congruence, for date 570 11 30, the weekday is Friday
According to Myrddin's lib, for date 1066 9 25, the weekday is Sunday
According to Zeller's congruence, for date 1066 9 25, the weekday is Tuesday
According to Myrddin's lib, for date 1776 7 04, the weekday is Sunday
According to Zeller's congruence, for date 1776 7 04, the weekday is Thursday
According to Myrddin's lib, for date 1933 1 30, the weekday is Sunday
According to Zeller's congruence, for date 1933 1 30, the weekday is Monday
According to Myrddin's lib, for date 1953 3 06, the weekday is Sunday
According to Zeller's congruence, for date 1953 3 06, the weekday is Friday
According to Myrddin's lib, for date 2100 1 09, the weekday is Sunday
According to Zeller's congruence, for date 2100 1 09, the weekday is Saturday
According to Myrddin's lib, for date 2202 12 15, the weekday is Sunday
According to Zeller's congruence, for date 2202 12 15, the weekday is Wednesday
According to Myrddin's lib, for date 7032 3 26, the weekday is Sunday
According to Zeller's congruence, for date 7032 3 26, the weekday is Monday
*/

// In Myrddin: https://eigenstate.org/myrddin/
// See: https://www.reddit.com/r/dailyprogrammer/comments/79npf9/20171030_challenge_338_easy_what_day_was_it_again/
// Run with: // PATH=/path/to/myrddin/bin:$PATH mbld -b /tmp/dp dp_338_easy_what_day.myr && /tmp/dp "2017 10 30" "2016 2 29" "2015 2 28" "29 4 12" "570 11 30" "1066 9 25" "1776 7 04" "1933 1 30" "1953 3 06" "2100 1 09" "2202 12 15" "7032 3 26"
